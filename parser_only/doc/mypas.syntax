Syntax definitions for the academic project My Pascal,
which is a simplified / modified Pascal compiler,
according to the following EBNF grammar:

Start symbol:: mypas

Production Rules::

mypas -> PROGRAM ID ; header body .


Declarative scope::

header -> varmodel { procmodel | funcmodel }

varmodel -> [ VAR  vargroup { vargroup } ]

vargroup -> varlist : typemodif ;

varlist -> ID { , ID }

typemodif -> BOOLEAN | INTEGER | REAL | DOUBLE

procmodel -> PROCEDURE ID formalparms ; header body ;

funcmodel -> FUNCTION ID formalparms : typemodif ; header body ;

formalparms -> [ ( arglist ) ]

arglist -> argmodel { ; argmodel }

argmodel -> [ VAR ] varlist : typemodif


Imperative scope::

body -> BEGIN stmtlist END

stmtlist -> stmt { ; stmt }

stmt ->   body
	| ifstmt
	| whlstmt
	| repstmt
	| smpexpr
	| <empty>

ifstmt -> IF expr THEN stmt [ ELSE stmt ]

expr -> smpexpr [ relop smpexpr ]

smpexpr -> [ + | - ] term { oplus term }

term -> factor { otimes factor }

factor ->   ID [ ":=" expr  | ( exprlist ) ]
	  | UINT
	  | FLTP
	  | ( expr )

exprlist -> expr { , expr }

whlstmt -> WHILE expr DO stmt

repstmt -> REPEAT stmtlist UNTIL expr

